plugins {
    id "com.android.application"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

    defaultConfig {
        applicationId "com.sketchware.remod"
        namespace "com.sketchware.remod"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdkVersion 28
        versionCode 150
        versionName "v6.4.0-rc03"

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "FLAVOR_NAME_WITH_AABS", "\"withAabOutputSupport\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS", "\"withoutAabOutputSupport\"")
    }

    flavorDimensions "api"
    productFlavors {
        withAabOutputSupport {
            dimension "api"
            minSdkVersion 26
            versionNameSuffix " with AAB support"
        }

        withoutAabOutputSupport {
            dimension "api"
            minSdkVersion 21
            versionNameSuffix " without AAB support"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    androidResources {
        additionalParameters "--stable-ids", "public-stable-ids.txt"
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.aar"])
    implementation fileTree(dir: "libs", include: ["*.jar"], excludes: ["bundletool-*"])

    withoutAabOutputSupportImplementation "com.android.tools:annotations:30.1.3"
    withoutAabOutputSupportImplementation "com.google.guava:guava:30.1-jre"

    withAabOutputSupportImplementation fileTree(dir: "libs", include: "bundletool-*")

    def codeEditorVersion = "0.11.1"
    def excludeAnnotation = { exclude group: "androidx.annotation", module: "annotation" }
    def editorGroupId = "io.github.Rosemoe.sora-editor"

    implementation "$editorGroupId:editor:$codeEditorVersion", excludeAnnotation
    implementation "$editorGroupId:language-java:$codeEditorVersion", excludeAnnotation
    implementation("$editorGroupId:language-textmate:$codeEditorVersion") {
        exclude group: "com.google.code.gson", module: "gson"
    }

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.angads25:filepicker:1.1.1"

    implementation "com.android.tools:r8:3.3.28"

    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "com.squareup.okio:okio:1.14.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
}
